plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.6.0'
    id 'org.jetbrains.changelog' version '1.3.1'
}

group 'hdzi'
version '3.1.0'


sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven { url = 'https://maven.aliyun.com/repository/public' }
    //jcenter()
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

// 兼容 Apple M1
if ('Mac OS X' == System.getProperty('os.name') && 'aarch64' == System.getProperty('os.arch')) {
    // 2019.1对应的jbr并没有M1的版本,请求下载jbr会403,改成amd64的jbr版本
    System.setProperty('os.arch', 'amd64')
}

intellij {
    version = '2020.1.1'
    type = 'IC'
    plugins = ['java', 'maven', 'gradle', 'Groovy', 'Kotlin']
}

changelog {
    headerParserRegex = ~/\d+\.\d+\.\d+/
    groups = []
}

patchPluginXml {
    sinceBuild = '201'
    untilBuild = ''

    changeNotes = """
${changelog.get(project.version.replaceAll(~/^(\d+\.\d+\.\d+)(-.+)?$/, '$1')).toHTML()}
<a href="https://github.com/hdzitao/idea-editstarters-plugin/blob/3.x/CHANGELOG.md">ChangeLog</a>
      """
}

//test.enabled = false